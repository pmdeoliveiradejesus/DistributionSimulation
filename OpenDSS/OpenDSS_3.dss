// Author Paulo M. De Oliveira pm.deoliveiradejes@uniandes.edu.co March 15, 2018
! Kersting NEV Test system
! W. H. Kersting, A three-phase unbalanced line model with grounded
! neutrals through a resistance, 2008 Ieee Power and Energy Society General
! Meeting, Vols 1-11 (2008) 2651-2652
! n=1
! 3 phase approach (kron's reduction)
clear
! **** DEFINE SOURCE BUS
new circuit.KersNEV2nThreeP basekV=12.47 phases=3  !Define a 3-phase source
~ mvasc3=500   mvasc1=500 x1r1=3 x0r0=3
! **** DEFINE DISTRIBUTION LINE
set earthmodel=carson
! **** DEFINE WIRE DATA  STRUCTURE
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=5.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=5.563 
! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL WITH KRON
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 
! **** 12.47 KV LINE!
new line.line1 geometry=4wire length=6000 units=ft bus1=sourcebus.1.2.3 bus2=n2.1.2.3
! **** DEFINE 3-PHASE UNBALANCED LOADS
New Load.n2.1 Phases=1  Bus1=n2.1   kVA=3000 pf=0.9  kV=12.47 conn=wye  vminpu=0.1 vmaxpu=1.9
New Load.n2.2 Phases=1  Bus1=n2.2   kVA=3500 pf=0.95   kV=12.47  conn=wye  vminpu=0.1 vmaxpu=1.9
New Load.n2.3 Phases=1  Bus1=n2.3   kVA=2500 pf=0.85  kV=12.47 conn=wye vminpu=0.1 vmaxpu=1.9
!New Fault.faseA   Phases=1  Bus1=n2.1  Bus2=n2.0
!Save Circuit Dir=MyNewCircuitName
solve
! Various reports ...
! show voltages LN Nodes 
! show currents resid=yes elements  ! this shows sum of phase currents!
! Show Powers kva Elements

